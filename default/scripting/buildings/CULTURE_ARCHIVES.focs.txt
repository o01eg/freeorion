BuildingType
    name = "BLD_CULTURE_ARCHIVES"
    description = "BLD_CULTURE_ARCHIVES_DESC"
    buildcost = 200 * [[BUILDING_COST_MULTIPLIER]]
    buildtime = 1
    tags = "ANTIQUATED"
    location = Described description = "NOWHERE" condition = Not All
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_BUILDING_STABILITY_EFFECTS]]
    
        EffectsGroup          // These bonuses given to anyone
            scope = And [
                Object id = Source.PlanetID
                Planet
                NOT Population high = 0
                OwnedBy empire = Source.Owner
            ]
            priority = [[VERY_LATE_PRIORITY]]
            effects = [
                SetTargetResearch value = Value + 5
                SetTargetIndustry value = Value + Target.Population / 2
            ]

        EffectsGroup
            scope = And [
                Object id = Source.PlanetID
                Planet
                OwnedBy empire = Source.Owner
                Not HasSpecial name = "HEAD_ON_A_SPIKE_SPECIAL"
            ]
            activation = Not ProducedByEmpire empire = Source.Owner
            effects = [
                AddSpecial name = "HEAD_ON_A_SPIKE_SPECIAL"
                GenerateSitRepMessage
                    message = "HEAD_ON_A_SPIKE_MESSAGE"
                    label = "HEAD_ON_A_SPIKE_MESSAGE_LABEL"
                    icon = "icons/sitrep/empire_eliminated.png"
                    parameters = [
                        tag = "empire" data = Source.ProducedByEmpireID
                    ]
            ]
    ]
    icon = "icons/building/archive.png"

#include "/scripting/common/priorities.macros"
#include "/scripting/common/base_prod.macros"
#include "/scripting/buildings/buildings.macros"
