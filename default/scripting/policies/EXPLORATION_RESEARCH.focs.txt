Policy
    name = "PLC_EXPLORATION_RESEARCH"
    description = "PLC_EXPLORATION_RESEARCH_DESC"
    short_description = "PLC_EXPLORATION_RESEARCH_SHORT_DESC"
    category = "MILITARY_CATEGORY"
    adoptioncost = 5
    exclusions = [ "PLC_ALGORITHMIC_RESEARCH" ]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        // spread research bonus even over all ships that explore system on the same turn
        EffectsGroup
            scope = And [
                Ship
                OwnedBy empire = Source.Owner
                InSystem
                ContainedBy And [
                    System
                    (CurrentTurn == TurnSystemExplored empire = Source.Owner id = LocalCandidate.ID)
                ]
            ]
            effects = SetTargetResearch value = Value +
                (NamedReal name = "EXPLORATION_RESEARCH_BONUS_TOTAL" value = 1.0) / max(1.0,
                    Statistic Count condition = And [
                        InSystem id = Target.SystemID
                        Ship
                        OwnedBy empire = Source.Owner
                    ])
        
        EffectsGroup
            scope = And [
                System
                (CurrentTurn == TurnSystemExplored empire = Source.Owner id = LocalCandidate.ID)
                Contains And [
                    Ship
                    OwnedBy empire = Source.Owner
                ]
            ]
            effects =
                GenerateSitRepMessage
                    message = "SITREP_FIELD_RESEARCH"
                    label = "SITREP_FIELD_RESEARCH_LABEL"
                    icon = "icons/tech/environmental_encapsulation.png"
                    parameters = tag = "system" data = Target.SystemID
                    empire = Source.Owner
    ]
    graphic = "icons/policies/exploration_research.png"

#include "/scripting/policies/policies.macros"
#include "/scripting/common/priorities.macros"
